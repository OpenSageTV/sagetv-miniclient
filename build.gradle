buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'org.robovm:robovm-gradle-plugin:1.6.0'
    }
}

def getVersionFile(project) {
    def ver = new File('core/src/main/java/sagex/miniclient/Version.java');
    if (!ver.exists()) {
        ver = new File('../core/src/main/java/sagex/miniclient/Version.java');
    }
    if (!ver.exists()) {
        ver = new File('src/main/java/sagex/miniclient/Version.java');
    }
    if (!ver.exists()) {
        ver = new File(project.rootDir, 'core/src/main/java/sagex/miniclient/Version.java');
    }
    if (!ver.exists()) {
        throw new RuntimeException("Can't find VERSION from " + new File(".").getAbsolutePath() + " with project " + project.rootDir);
    }
    return ver;
}

allprojects {
    version = '1.0'
    ext {
        appName = "SageTVMiniclient"
        versionText = getVersionFile(project).text
        baseVersion = (versionText =~ /VERSION = "([^"]+)"/)[0][1]
        //System.out.println("Version: " + baseVersion)
        gdxVersion = "1.9.8"
        // NOTE: we can't use gradle builds for ijk because it uses a limited profile
        ijkVersionDev = "0.8.8-SNAPSHOT";
        ijkVersion = "0.7.5"; // NOT USED

        // NOTE: using exoplayer from mavenlocal (prebuilt) for ffmpeg
        exoVersionCustomExt = '2.11.5'
        // NOTE: using regular exoplayer for core stuff
        exoVersion = '2.11.5'

        androidExtrasVer = "28.0.0"
        androidBuildToolsVersion = "28.0.3"

        androidMinSdkVersion = 21
        androidCompileSdkVersion = 28
        androidTargetSdkVersion = 28

        appVersionCode = 2101055
        appVersionName = "${baseVersion}"

        // tell every project where to find the local project maven files
        mavenInProject = file('../mavenlocal').absoluteFile.toURI()

        defJavaVesionNum = 1.7
        defJavaVesionConst = JavaVersion.VERSION_1_7
    }

    repositories {
        maven { url mavenInProject }
        mavenLocal()
        google()
        mavenCentral()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

